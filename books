#!/usr/bin/env zsh
#
# Wrapper around `task` to use it as a book diary management script.
#
# Uses UDFs for author, gender, genre.
# Task description == title, and annotations are used for notes.
#
# Intended usage is:
#   books add author:"C.J. Cherryh" genre:sf gender:f +reread Foreigner
#   ...go off and read it...
#   books 1 done
#   books annote "I really enjoyed this."
#   books bookstats genre
#
### New filters:
# year:XXXX
#   must be the first argument! equivalent to:
#   end.after:XXXX-01-01 end.before:(XXXX+1)-01-01
#
### New commands:
# books reset-config
# books grep <genre, author, or title regex>
# books <filter> bookstats <list of fields>
#   displays counted stats of the different field values, e.g.
#   `books year:2017 genre` shows you what % of books you read in 2017 were
#   what genre.
# books annote <text>
#   annotates the most recently finished book.
#
# TODO:
# - figure out if series tracking is useful, and if so, how (project field? UDA?)
# - figure out a good way to track booklord challenge categories
# - figure out how to normalize the genre tree

set -e
source $(dirname $(realpath $0))/tasklib.sh

export TASKRC=$HOME/Books/booklogrc

function main {
  task/init-config books.defaults
  task/dispatch "$@"
}

task/register grep '^grep' books/grep <<EOF

  $NAME grep <pattern> [report]

Display books where the author or description match the given pattern (as a
case-insensitive regex) in the given report format; if not specified, the report
defaults to "all".
EOF
function books/grep {
  shift
  \task rc.verbose:label,blank rc.regex:yes rc.search.case.sensitive:no \
    "( author ~ '$1' or description ~ '$1' )" "${2:=all}"
}

task/register annote '^annote' books/annote <<EOF

  $NAME annote <text>

Annotate the most recently finished book with the given text and the current
timestamp.
EOF
function books/annote {
  LAST=$(\task rc.verbose:nothing rc.report.one_field.sort:end+ one_field | tail -n1)
  shift
  \task $LAST annotate "$@"
}

task/register cc '^cc' books/-cc <<EOF

  $NAME cc ...

Alias for \`booklord ...\`.
EOF
function books/-cc {
  shift
  exec booklord "$@"
}

task/register bookstats 'bookstats' books/bookstats <<EOF

  $NAME [filter] bookstats <field ...>

For all books that match the filter, report:
- the total number of books
- the number of unique authors
- all values of all listed fields, sorted by frequency, along with their counts
  and what % of all books in the filter have that value
Multivalued fields (e.g. books with multiple genres separated by &) will be
counted once for each value, so the total count for a given field may exceed
the number of books matched by the filter.
EOF
function books/bookstats {
  local FILTER=()
  while [[ $1 ]]; do
    if [[ $1 == bookstats ]]; then
      shift; break;
    fi
    FILTER+="$1"
    shift
  done

  # Get number of books to use as a basis for the count.
  NROF=$(\task "${FILTER[@]}" count)
  NROF_AUTHORS=$(\task rc.verbose:nothing rc.report.one_field.columns:author "${FILTER[@]}" one_field | sort | uniq | wc -l)
  printf '\n\x1B[1m%24s  %-4d (by %d authors)\x1B[0m\n' "BOOKS" "$NROF" "$NROF_AUTHORS"

  # Display prefix only if we have more than one field to collect stats for.
  local FIELD_PREFIX=''
  if (( $# > 1)); then
    FIELD_PREFIX=yes
  fi

  # Get number of occurrences of each value of each field.
  local SPLIT
  for field in "$@"; do
    FIELD_PREFIX="${FIELD_PREFIX:+$field:}"

    if [[ $field == "tags" ]]; then
      FIELD_PREFIX="${FIELD_PREFIX:++}"
      SPLIT=' '
    else
      SPLIT=' & '
    fi

    \task rc.verbose:nothing rc.report.one_field.columns:"$field" "${FILTER[@]}" one_field \
      | sed -E "s,$SPLIT,\n,g" | sort | uniq -c | sed -E "s,( +[0-9]+ +),\1${FIELD_PREFIX},"
  done | sort -r -g | while read count field; do
    printf '%24s  %-4d (%3.2f%%)\n' "$field" "$count" "$(\task calc "100.0*$count/$NROF")"
  done
}

exec main "$@"
