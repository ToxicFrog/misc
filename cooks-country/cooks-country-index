#!/usr/bin/env zsh
# Fetch/update a recipe index for Cook's Country.

# Issue: title, cover image, and list of recipes.
# Recipe: title, blurb, image, "why it works"
# UI: a list of issues; click on an issue to expand and display the list of recipes with blurbs.
# search box at the top; typing in a search automatically filters to only recipes matching the search and issues containing those recipes, and automatically expands those issues.

set -e

OUTPUT=/srv/www/archives/cooks-country/

# attr html field -> returns contents of field
function attr {
  print -r "$1" | egrep -o " $2=\"[^\"]+\"" | cut -d'"' -f2 | head -n1
}

function sanitize {
  print -r "$1" | tr ' /\t' '_'
}

function html {
  >>"$OUTPUT/index.html" printf "$@"
}

function unescape {
  print -r "<div>$*</div>" | wring text - //div
}

mkdir -p "$OUTPUT"

CSS=$(dirname $0)/cooks-country-index.css
JS=$(dirname $0)/cooks-country-index.js

cp $CSS $OUTPUT/style.css

echo "Fetching master index..."
issues="$(curl -s 'https://www.cookscountry.com/magazines/browse' | wring html - .result__image-link)"
# issues="$(cat /tmp/ck/browse)"
print -r "$issues" | tac | while read -r issue; do
  title="$(attr "$issue" title)"
  url="$(attr "$issue" href)"
  img="$(attr "$issue" data-src)"

  out="$OUTPUT/$(sanitize "$title")"
  [[ -f "$out/done" ]] && continue;

  echo -n "Fetching $title:"
  rm -f "$out/index"
  mkdir -p "$out"
  print -r "$title" > "$out/title"

  # Fetch cover if missing
  [[ -f "$out/cover.jpeg" ]] || {
    echo -n " cover"
    curl -s "$img" > "$out/cover.jpeg"
    echo -n "."
  }

  # Fetch recipe images and write issue index file
  recipes="$(curl -s "https://www.cookscountry.com$url" \
    | wring html - .recipe \
    | fgrep -v result__online-extra \
    | lua -e 'print((io.read("*a"):gsub("\r",""):gsub("([^>])\n+", "%1 ")))')"
  echo -n " recipes"
  print -r "$recipes" | while read -r recipe; do
    title="$(attr "$recipe" title)"
    title="$(unescape "$title")"
    img="$(attr "$recipe" data-src)"
    desc="$(print -r "$recipe" | wring text - .result__description)"
    splash="$(sanitize "$title").jpeg"
    [[ -f "$out/$splash" ]] || {
      if [[ $img ]] then
        curl -s "$img" > "$out/$splash"
      else
        curl -s "https://cataas.com/cat?width=120&height=120" > "$out/$splash"
      fi
    }
    printf '%s\t%s\t%s\n' "$splash" "$title" "$desc" >> "$out/index"
    echo -n "."
  done

  if [[ $(cat $out/index | wc -l) -lt 4 ]]; then
    # An issue with few/no recipes? Probably this is one of those cases where
    # they return multiple copies of an issue in the search, but only one of them
    # is valid.
    echo $out
    ls $out
    #rm -f "$out/index" "$out/done" "$out/cover.jpeg"
    echo "error!"
    exit 1
    continue
  fi

  touch "$out/done"
  echo ""
  # break
done

echo -n "Generating top-level HTML"
rm -rf "$OUTPUT/index.html"
html "
  <html>
    <head>
      <title>Cook's Country Index</title>
      <link rel=\"stylesheet\" type=\"text/css\" href=\"./style.css\">
      <meta charset=\"UTF-8\">
    </head>
      <script>
        $(cat $JS)
      </script>
    <body>
"

ls -t "$OUTPUT" | fgrep -v index.html | fgrep -v style.css | while read -r issue; do
  # [[ -f "$issue/done" ]] || continue
  html '
    <div class="header">
      <img src="%s" width=120px>
      <div class="header-title">%s</div>
    </div>
    <table>
  ' "$issue/cover.jpeg" "$(cat "$OUTPUT/$issue/title")"
  cat "$OUTPUT/$issue/index" | while IFS=$'\t' read -r splash title desc; do
    splash="${splash//\"/%22}"
    id="$issue/${splash%.*}"
    html '
      <tr id="%s" class="recipe-row">
        <td class="hotlist-button" onclick="toggleStar(this)">‚òÜ</td>
        <td class="recipe-name" width=100%%>%s<br>%s</td>
        <td style="text-align:right;">
          <img src="%s" height="80px" loading="lazy">
        </td>
        <td nowrap>
          <span class="cooked-checkbox">
            <input id="cb:cooked:%s" type="checkbox" onchange="toggleState(this, '\''cooked'\'');">
            <label>üçΩ</label>
          </span>
          <br>
          <span class="written-checkbox">
            <input id="cb:written:%s" type="checkbox" onchange="toggleState(this, '\''written'\'');">
            <label>üóí</label>
          </span>
          <br>
          <span class="hidden-checkbox">
            <input id="cb:hidden:%s" type="checkbox" onchange="toggleState(this, '\''hidden'\'');">
            <label>‚äó</label>
          </span>
        </td>
      </tr>
    ' "$id" "$title" "$desc" "$issue/$splash" "$id" "$id" "$id"
  done
  html '</table>'
  echo -n "."
  # ;break
done
html "</body></html>"
echo ""
